// License Information:
// ApolloGame/Music.NeilBundle/SubMusic.neil
// Music
// version: 21.06.10
// Copyright (C) 2020, 2021 Jeroen P. Broks
// This software is provided 'as-is', without any express or implied
// warranty.  In no event will the authors be held liable for any damages
// arising from the use of this software.
// Permission is granted to anyone to use this software for any purpose,
// including commercial applications, and to alter it and redistribute it
// freely, subject to the following restrictions:
// 1. The origin of this software must not be misrepresented; you must not
// claim that you wrote the original software. If you use this software
// in a product, an acknowledgment in the product documentation would be
// appreciated but is not required.
// 2. Altered source versions must be plainly marked as such, and must not be
// misrepresented as being the original software.
// 3. This notice may not be removed or altered from any source distribution.
// End License Information

#macro PlayChannel 1

group _SubMusic

	string Playing
	var MusObject
	table pushed = {}
	Int _MusicVolume = 100
	
	Set Int MusicVolume 
		Audio.Volume(PlayChannel,value)
		_MusicVolume=value
	End
	
	Get Int MusicVolume
		Return _MusicVolume
	End
	
	Void FadeTo(Int nv)
		While nv>MusicVolume
			MusicVolume++
			FPS.WaitMinTicks()
			Events.Poll()
		End
		While nv<MusicVolume
			MusicVolume--
			FPS.WaitMinTicks()
			Events.Poll()
		End
	End
	
	void Play(string File)
		Audio.Chat=true // Debug
		File=File:upper()
		if Playing==File
			return
		end
		cout("Loading music: ",file,"\n")
		Static Int Num
		Num = (Num+1)%2		
		MusObject = Audio.Load(File,"LIB_MUSIC__"..Num)
		Playing=File
		MusObject.Play(PlayChannel,true)
		print("Playing on channel: PlayChannel")
		MusicVolume = 100
	end
	
	void Stop()
		if Playing==""
			return
		end
		Print("Stopping music on channel: PlayChannel")
		Audio.Stop(PlayChannel)
		Playing=""
	end
	
	void Push()
		if Playing==""
			return
		end
		pushed += Playing
	end
	
	void Pop()
		if #pushed==0
			return
		end
		string popped = pushed[#pushed]
		pushed[#pushed]=nil
		Play(Popped)
	end
	
	void PopVoid()
		if #pushed==0
			return
		end
		string popped = pushed[#pushed]
		pushed[#pushed]=nil
	end
end